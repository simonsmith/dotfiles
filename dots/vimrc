"  Vim plug
" https://github.com/junegunn/vim-plug
" ------------------------------------------------------------------------------

if has('nvim')
  let g:plugin_path = '~/.config/nvim/plugged'
  let $NVIM_TUI_ENABLE_TRUE_COLOR = 1
  let $NVIM_TUI_ENABLE_CURSOR_SHAPE = 1
else
  let g:plugin_path = '~/.vim/plugged'
  set nocompatible
endif

call plug#begin(g:plugin_path)

" Theme
" ------------------------------------------------------------------------------

" Atom theme
Plug 'joshdick/onedark.vim'
Plug 'joshdick/airline-onedark.vim'

" tmux
" ------------------------------------------------------------------------------

if executable('tmux')
  " Navigate between vim and tmux splits
  Plug 'christoomey/vim-tmux-navigator'
  " Restore `FocusLost` and `FocusGained` events in tmux
  Plug 'tmux-plugins/vim-tmux-focus-events'
  " tmux statusline generator
  Plug 'edkolev/tmuxline.vim'
endif

" Finding and replacing
" ------------------------------------------------------------------------------

" Run Ack from inside Vim
Plug 'mileszs/ack.vim'
" Execute operations on the files in the quickfix list
Plug 'henrik/vim-qargs'
" Fuzzy finding anything
Plug 'junegunn/fzf', {'dir': '~/.fzf', 'do': './install --all'}
" fzf integration for Vim
Plug 'junegunn/fzf.vim'
" Improved search in Vim
Plug 'junegunn/vim-pseudocl' | Plug 'junegunn/vim-oblique'

" File management
" ------------------------------------------------------------------------------

" File browser
Plug 'scrooloose/nerdtree'
" Show git changes in NERDTree
Plug 'Xuyuanp/nerdtree-git-plugin'
" Easy directory browsing with `-`, integrates with NERDTree
Plug 'tpope/vim-vinegar'

" Buffers
" ------------------------------------------------------------------------------

" Close all buffers except current
Plug 'schickling/vim-bufonly', {'on': 'BufOnly'}
" Close buffers without losing window layout
Plug 'moll/vim-bbye'

" Language pack
" ------------------------------------------------------------------------------

" Syntax and highlighting for every language
Plug 'sheerun/vim-polyglot'
Plug 'blockloop/vim-swigjs'

" Tags
" ------------------------------------------------------------------------------

" Generates ctags automatically
Plug 'ludovicchabant/vim-gutentags'

" Editing
" ------------------------------------------------------------------------------

" Insert or delete brackets, parens, quotes in pair.
Plug 'jiangmiao/auto-pairs'
" Display indent levels
Plug 'nathanaelkane/vim-indent-guides', {'on': 'IndentGuidesToggle'}
" Allow plugins to be repeated
Plug 'tpope/vim-repeat'
" Comment out blocks of code
Plug 'tpope/vim-commentary'
" Toggle location and quickfix lists
Plug 'Valloric/ListToggle'
" Highlight characters for motions (f,F,t,T)
Plug 'unblevable/quick-scope'
" Advanced movement similar to motions
Plug 'justinmk/vim-sneak'
" Correct common typos
Plug 'chip/vim-fat-finger'
" Keep folds performant
Plug 'Konfekt/FastFold'
" Move between windows and allow swapping position
Plug 't9md/vim-choosewin'
" Remember cursor position in closed buffers
Plug 'dietsche/vim-lastplace'

" Text objects
" ------------------------------------------------------------------------------

" Improve text objects for arguments, brackets etc
Plug 'wellle/targets.vim'

" Add various text objects
Plug 'kana/vim-textobj-user'
Plug 'kana/vim-textobj-function'
Plug 'kana/vim-textobj-entire'
Plug 'kana/vim-textobj-line'
Plug 'thinca/vim-textobj-function-javascript', {'for': ['javascript', 'javascript.jsx', 'json']}
Plug 'whatyouhide/vim-textobj-erb', {'for': 'eruby'}
Plug 'jasonlong/vim-textobj-css', {'for': ['css', 'scss']}

" General
" ------------------------------------------------------------------------------

" Set spaces and other settings on a per project basis
Plug 'editorconfig/editorconfig-vim'
" GUI for the undo tree
Plug 'mbbill/undotree', {'on': 'UndotreeToggle'}
" Switch the cwd based on the file location
Plug 'airblade/vim-rooter'

" Refactoring
" ------------------------------------------------------------------------------

" Split and join HTML tags, JS objects etc
Plug 'AndrewRadev/splitjoin.vim'
" Make it easy to add/remove/change brackets, quotes etc
Plug 'tpope/vim-surround'

" Copy/Paste behaviour
" ------------------------------------------------------------------------------

" Stop everything saving to a register and instead rely on just `m` for cut
Plug 'svermeulen/vim-easyclip'
" Improve pasting code from the clipboard
Plug 'ConradIrwin/vim-bracketed-paste'

" Sessions
" ------------------------------------------------------------------------------

Plug 'xolox/vim-misc'
Plug 'xolox/vim-session'

" Status bar
" ------------------------------------------------------------------------------
"
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'

" Tabs
" ------------------------------------------------------------------------------

" Make managing tabs easier with rename, add, delete etc
Plug 'gcmt/taboo.vim'
" Allow a window to be fully viewed. Useful when multiple splits are in use
Plug 'troydm/zoomwintab.vim'

" Snippets
" ------------------------------------------------------------------------------

Plug 'SirVer/ultisnips'
Plug 'honza/vim-snippets'

" Git
" ------------------------------------------------------------------------------

Plug 'tpope/vim-fugitive'
Plug 'mhinz/vim-signify'

" HTML/CSS
" ------------------------------------------------------------------------------

" Highlight css colors with their color
Plug 'ap/vim-css-color', {'for': ['css', 'scss']}
" Easy HTML and CSS generation
Plug 'mattn/emmet-vim'

" Automatically add a closing HTML tag
Plug 'alvan/vim-closetag'

" JavaScript
" ------------------------------------------------------------------------------

Plug 'othree/javascript-libraries-syntax.vim'
Plug 'mxw/vim-jsx', {'for': ['javascript', 'javascript.jsx', 'json']}
Plug 'moll/vim-node', {'for': ['javascript', 'javascript.jsx', 'json']}
Plug 'elzr/vim-json', {'for': ['javascript', 'javascript.jsx', 'json']}
Plug 'jason0x43/vim-js-indent', {'for': ['javascript', 'javascript.jsx', 'json']}

if has('nvim')
  " Linting
  Plug 'benekastah/neomake'
  Plug 'benjie/neomake-local-eslint.vim'
  " Autocomplete
  Plug 'Shougo/deoplete.nvim'
  Plug 'carlitux/deoplete-ternjs'
else
  Plug 'sjl/vitality.vim'
endif

" Devicons have to come after other plugins
Plug 'ryanoasis/vim-devicons'

call plug#end()

" Shortcut for checking if a plugin is loaded
function! s:has_plugin(plugin)
  let lookup = 'g:plugs["' . a:plugin . '"]'
  return exists(lookup)
endfunction

"  General config
" ------------------------------------------------------------------------------

" Theme
if !has('nvim')
  let g:onedark_termcolors=16
endif
set background=dark
silent! colorscheme onedark

" Treat <li> and <p> tags like the block tags they are
let g:html_indent_tags='li\|p'

" Set <space> to leader
let mapleader=' '

" Turn off swapfiles
set noswapfile
set nobackup
set nowb

" Allow editing of binary files
" Must be set before expandtab
" http://stackoverflow.com/a/26901774
set binary

" Make it obvious where 80 characters is
set textwidth=80
set colorcolumn=+1

" Use ``indent`` based folding
set foldmethod=indent
" Disable all folds on file open until `zc` or `zM` etc is used
set nofoldenable

" With :set hidden, opening a new file when the current buffer has unsaved
" changes causes files to be hidden instead of closed
set hidden

" Allow tab names to be remembered in sessions
set sessionoptions+=tabpages,globals

" Briefly move the cursor to the matching brace
set showmatch

" Lazyredraw attempts to solve Vim lag by reducing the amount of
" processing required. When enabled, any action that is not typed
" will not cause the screen to redraw
set lazyredraw

" Don't display the current mode (Insert, Visual, Replace)
" in the status line. This info is already shown in the
" Airline status bar.
set noshowmode

" Stop vim trying to syntax highlight long lines, typically found in minified
" files. This greatly reduces lag yet is still wide enough for large displays
set synmaxcol=500

" Highlight current line
set cursorline

" Indent using two spaces.
set softtabstop=2
set shiftwidth=2

" Smoother scrolling when moving horizontally
set sidescroll=1

" Use spaces, not tabs
set expandtab

" Don't scan included files. The .tags file is more performant.
set complete-=i
set complete+=kspell

" Prevent autocomplete options opening in scratchpad
set completeopt-=preview

" This is set low so that gitgutter updates reasonably quickly
" https://github.com/airblade/vim-gitgutter#when-are-the-signs-updated
set updatetime=300

" Don't wrap lines
set nowrap

" Keep the cursor in the same place when switching buffers
set nostartofline

" Show col and line position in the statusbar
set ruler

" Open new split panes to right and bottom, which feels more natural
set splitbelow
set splitright

" Ignore case of searches
set ignorecase

" If a pattern contains an uppercase letter
" it is case sensitive, otherwise, it is not
set smartcase

" Highlight tailing whitespace
set list listchars=tab:\ \ ,trail:·

" Use the OS clipboard by default (on versions compiled with `+clipboard`)
set clipboard=unnamed

" Spell check
set spelllang=en
highlight SpellBad term=standout ctermbg=red ctermfg=white term=underline cterm=underline
highlight clear SpellCap
highlight SpellCap term=underline cterm=underline
highlight clear SpellRare
highlight SpellRare term=underline cterm=underline
highlight clear SpellLocal
highlight SpellLocal term=underline cterm=underline

" Where it should get the dictionary files
let g:spellfile_URL = 'http://ftp.vim.org/vim/runtime/spell'

" For autocompletion, complete as much as you can.
set wildmode=list:longest,full

" don't add extra space after ., !, etc. when joining
set nojoinspaces

" Allow cursor keys in insert mode
set esckeys

" Eliminate delay when switching modes
set ttimeoutlen=0

" Auto save changes before switching buffer
set autowrite

" Add the g flag to search/replace by default
set gdefault

" Centralize backups, swapfiles and undo history
set backupdir=~/.config/nvim/backups
set directory=.,$TEMP " Stop the swp file warning

if has("persistent_undo")
  set undodir=~/.config/nvim/undo
  set undofile
  set undolevels=1000
  set undoreload=10000
endif

" Don’t create backups when editing files in certain directories
set backupskip=/tmp/*,/private/tmp/*

" Respect modeline in files
set modeline
set modelines=4

" Enable per-directory .vimrc files and disable unsafe commands in them
set exrc
set secure

" Show “invisible” characters
set lcs=tab:▸\ ,trail:·,nbsp:_
set list

" Disable error bells
set noerrorbells

" Don't show the intro message when starting Vim
" Also suppress several 'Press Enter to continue messages' especially
" with FZF
set shortmess=aoOtI

" Show the current mode
set title

" Show the (partial) command as it’s being typed
set showcmd

" Line numbers
set number
set relativenumber

" Path and file extensions to look for when using `gf`
set suffixesadd+=.css,index.css
set path+=node_modules

" Start scrolling before cursor gets to the edge
set scrolloff=5
set sidescrolloff=15
set sidescroll=1

" Remap annoying mistakes to something useful
cnoreabbrev W! w!
cnoreabbrev Q! q!
cnoreabbrev q: :q
cnoreabbrev Qall! qall!
cnoreabbrev Wq wq
cnoreabbrev Wa wa
cnoreabbrev wQ wq
cnoreabbrev WQ wq
cnoreabbrev W w
cnoreabbrev Qa q
cnoreabbrev Qall qall

" Vim only
" These are set by default in neovim

if !has('nvim')
  " Allow vim to determine the type from the file contents
  filetype plugin indent on
  syntax on
  " Autoindent when starting new line, or using `o` or `O`.
  set autoindent
  " Auto reload files on change
  set autoread
  " Allow backspace in insert mode
  set backspace=indent,eol,start
  " Use UTF-8 without BOM
  set encoding=utf-8 nobomb
  " Delete comment character when joining commented lines
  set formatoptions+=j
  " Store more history (default is 50)
  set history=1000
  " Highlight searches
  set hlsearch
  set incsearch
  " Always show status line
  set laststatus=2
  " Enable mouse in all modes
  set mouse=a
  " Use 'shiftwidth' when using `<Tab>` in front of a line.
  " By default it's used only for shift commands (`<`, `>`).
  set smarttab
  " Optimize for fast terminal connections
  set ttyfast
  " Enhance command-line completion
  set wildmenu
endif

" Key mappings
" ------------------------------------------------------------------------------

" Save file
nnoremap <leader>w :w<cr>

" Force j and k to work on display lines
nnoremap k gk
nnoremap j gj

" Adjust window size with arrow keys
noremap <right> <C-w>>
noremap <left> <C-w><
noremap <up> <C-w>+
noremap <down> <C-w>-

" Clone paragraph
noremap cp yap<S-}>p

" Make `Y` work from the cursor to the end of line
nnoremap Y y$

" Reformat whole file and move back to original position
nnoremap g= gg=G``

" use K to join current line with line above, just like J does with line below
nnoremap K kJ

" Automatically jump to end of pasted text
vnoremap <silent> y y`]
vnoremap <silent> p p`]
nnoremap <silent> p p`]

" Split
noremap <leader>x :<C-u>split<cr>
noremap <leader>v :<C-u>vsplit<cr>

" Switch buffers
nnoremap <silent> H :bp<CR>
nnoremap <silent> L :bn<CR>

" Spellcheck
map <F6> :setlocal spell!<cr>

" Clear search (highlight)
nnoremap <silent> <leader>k :noh<cr>

" Open new buffers
" Single window
nnoremap <leader>nb :enew<cr>

" Automatically 'gv' (go to previously selected visual block)
" after indenting or unindenting.
vnoremap < <gv
vnoremap > >gv

" Press enter for newline without insert
nmap <S-Enter> O<Esc>
nmap <cr> o<Esc>

" Allow quick edits and sourcing of vimrc
nnoremap <leader>ev :vsplit $MYVIMRC<cr>
nnoremap <leader>sv :source $MYVIMRC<cr>

" Search with Ack
nnoremap <leader>a :Ack!<space>-Q<space>

" Replace with Qdo
nnoremap <leader>r :Qdo<space> :%s/

" Open neovim terminal
if exists(':tnoremap')
  nmap <leader>t :term<cr>
endif

"  Plugin config
" ------------------------------------------------------------------------------

" Undotree
if s:has_plugin('undotree')
  let g:undotree_WindowLayout = 2

  nnoremap <f5> :UndotreeToggle<cr>
endif

" UltiSnips
if s:has_plugin('ultisnips')
  let g:UltiSnipsExpandTrigger="<tab>"
  let g:UltiSnipsJumpForwardTrigger="<tab>"
  let g:UltiSnipsJumpBackwardTrigger="<s-tab>"
endif

if s:has_plugin('vim-easyclip')
  let g:EasyClipUseSubstituteDefaults = 0
  " Use `gs` for substitute
  nmap <silent> gs <plug>SubstituteOverMotionMap
  nmap gss <plug>SubstituteLine
  xmap gs <plug>XEasyClipPaste
  imap <c-v> <plug>EasyClipInsertModePaste
  " Place a mark (m taken by easyclip)
  nnoremap gm m
endif

if s:has_plugin('MatchTagAlways')
  nnoremap <leader>% :MtaJumpToOtherTag<cr>
endif

" Gutentags
if s:has_plugin('vim-gutentags')
  let g:gutentags_tagfile = 'vim_tags'
  let g:gutentags_exclude = [
        \ 'node_modules',
        \ 'dist',
        \ 'vendor',
        \ 'bower_components']
endif

if s:has_plugin('vim-sneak')
  let g:sneak#s_next=1
  let g:sneak#use_ic_scs=0
endif

" Deoplete
if s:has_plugin('deoplete.nvim')
  let g:deoplete#enable_at_startup = 1
  let g:deoplete#auto_completion_start_length = 1
  let g:deoplete#sources = {}
  let g:deoplete#sources.python = ['ultisnips']
endif

" Deoplete tern
if s:has_plugin('deoplete-ternjs')
  let g:tern_show_signature_in_pum = 1
  let g:tern_request_timeout = 1
endif

" Sessions
if s:has_plugin('vim-session')
  let g:session_autosave = 'yes'
  let g:session_autoload = 'no'
endif

if s:has_plugin('vim-polyglot')
  let g:polyglot_disabled = ['json']
endif

" IndentGuides
if s:has_plugin('vim-indent-guides')
  " Add this here because this plugin is loaded ondemand so this mapping is not
  " present on initial load
  nmap <leader>ig :IndentGuidesToggle<cr>
endif

if s:has_plugin('vim-choosewin')
  nmap  <leader>g <plug>(choosewin)
  let g:choosewin_label_fill = 0
  let g:choosewin_blink_on_land = 0
endif

" JSX
if s:has_plugin('vim-jsx')
  let g:jsx_ext_required = 0
endif

if s:has_plugin('auto-pairs')
  let g:AutoPairsCenterLine = 0
endif

if s:has_plugin('vim-bbye')
  nnoremap <silent> Q :Bdelete<cr>
endif

" Taboo
if s:has_plugin('taboo.vim')
  let g:taboo_tabline = 0 " Let airline handle it all
  let g:taboo_renamed_tab_format = '%l%m'

  nnoremap th :tabprev<cr>
  nnoremap tl :tabnext<cr>
  nnoremap tn :tabnew<cr>
  nnoremap td :tabclose<cr>
  nnoremap to :TabooOpen<space>
  nnoremap tr :TabooRename<space>
endif

" NERD tree
if s:has_plugin('nerdtree')
  let g:NERDTreeWinSize = 30
  let NERDTreeIgnore = ['\.py[cd]$', '\~$', '\.swo$', '\.swp$', '^\.git$', '^\.hg$', '^\.svn$', '\.bzr$']
  let NERDTreeShowHidden = 1
  let NERDTreeHijackNetrw = 1
  let NERDTreeAutoDeleteBuffer = 1
endif

" fzf
if s:has_plugin('fzf.vim')
  let g:fzf_height = '30%'
  let g:fzf_commits_log_options = '--color --graph --pretty=format:"%C(yellow)%h%Creset -%C(auto)%d%Creset %s %C(bold blue)(%cr) %Cred<%an>%Creset" --abbrev-commit'

  nnoremap <c-p> :FZF<cr>
  nnoremap <c-i> :Buffers<cr>
  nnoremap <c-g> :BCommits<cr>
  nnoremap <c-t> :Tags<cr>
  nnoremap <leader><space> :Ag<cr>
  nmap <silent> <BS> :History:<cr>
  imap <c-x><c-f> <plug>(fzf-complete-file-ag)

  " Allow options to be passed to FZF :Ag
  " Probably quite hacky until a better solution appears
  " https://github.com/junegunn/fzf.vim/issues/92#issuecomment-191248596
  function! s:ag_with_opts(arg, bang)
    let tokens  = split(a:arg)
    let ag_opts = join(filter(copy(tokens), 'v:val =~ "^-"'))
    let query   = join(filter(copy(tokens), 'v:val !~ "^-"'))
    call fzf#vim#ag(query, '--hidden --ignore .git', a:bang ? {} : {'down': '40%'})
  endfunction

  if has('autocmd')
    autocmd VimEnter * command! -nargs=* -bang Ag call s:ag_with_opts(<q-args>, <bang>0)
  endif
endif

if s:has_plugin('ListToggle')
  let g:lt_height = 6
endif

" Ag
if executable('ag')
  " Use ag over grep
  set grepprg=ag\ --nogroup\ --nocolor
  " Use ag with ack.vim - https://github.com/ggreer/the_silver_searcher#vim
  let g:ackprg = 'ag --smart-case --nogroup --nocolor --column'
endif

" jq
if executable('jq')
  " JSON beautifier
  nnoremap <leader>z :%!jq '.'<cr>
endif

" vim-airline
if s:has_plugin('vim-airline')
  let g:airline_theme = 'onedark'
  let g:airline#extensions#branch#enabled = 1
  let g:airline_powerline_fonts = 1
  let g:airline#extensions#tmuxline#enabled = 0
  let g:airline#extensions#tabline#enabled = 1 " Enable the list of buffers
  let g:airline#extensions#tabline#fnamemod = ':t' " Show the filename
  let g:airline#extensions#tabline#fnamecollapse = 0
  let g:airline#extensions#tabline#tab_nr_type = 1 " Show tab number
  let g:airline#extensions#tabline#buffer_nr_show = 0
endif

" vim-signify
if s:has_plugin('vim-signify')
  let g:signify_vcs_list = ['git']
  let g:signify_update_on_focusgained = 1
  let g:signify_update_on_bufenter = 1
endif

" Neomake
if s:has_plugin('neomake')
  let g:neomake_error_sign = {'text': '✗', 'texthl': 'GitGutterDelete'}
  let g:neomake_warning_sign = {'text': '⚠', 'texthl': 'GitGutterChange'}
  let g:neomake_list_height = 6

  if has('autocmd')
    autocmd! BufWritePost,BufRead * Neomake
    autocmd! QuitPre * let g:neomake_verbose = 0
  endif
endif

" Tmuxline
if s:has_plugin('tmuxline.vim')
  let g:tmuxline_preset = {
        \'a'    : '#S',
        \'win'  : '#I #W',
        \'cwin' : '#I #W',
        \'y'    : ['#(spotify)', '#(tmux-mem-cpu-load -g 0 -m 1 -i 4)', '#(ipconfig getifaddr en0)', '#(battery -a -p -t)'],
        \'z'    : ['%R', '%d-%m-%Y'],
        \'options' : {'status-justify' : 'left'}}
endif

" JS lib syntax
if s:has_plugin('javascript-libraries-syntax.vim')
  let g:used_javascript_libs = 'underscore,jquery,react,requirejs,jasmine,chai'
endif

if s:has_plugin('quick-scope')
  let g:qs_highlight_on_keys = ['f', 'F', 't', 'T']
endif

if s:has_plugin('emmet-vim')
  imap hh <C-y>,
endif

" Automatic commands
" ------------------------------------------------------------------------------
"
if has('autocmd')
  " Open help in a vertical split
  augroup vimrc-help
    autocmd!
    autocmd BufEnter *.txt if &buftype == 'help' | wincmd L | setlocal relativenumber | endif
  augroup END

  augroup file-types
    autocmd!

    " Override some syntaxes so things look better
    autocmd BufNewFile,BufRead *.html setlocal syntax=swig
    autocmd BufNewFile,BufRead *.css setlocal syntax=scss
    autocmd BufNewFile,BufRead *.js setlocal syntax=javascript

    " Wrap text and turn on spell for markdown files
    autocmd BufNewFile,BufRead *.md setlocal wrap linebreak spell filetype=markdown

    " Automatically wrap at 72 characters and spell check git commit messages
    autocmd FileType gitcommit setlocal textwidth=72
    autocmd FileType gitcommit setlocal spell

    " Allow stylesheets to autocomplete hyphenated words
    autocmd FileType css,scss,sass setlocal iskeyword+=-
  augroup END

  " Switch between normal and relative line numbers and cursorline
  " when switching modes
  augroup highlight-when-switching-modes
    autocmd!
    autocmd InsertEnter * setlocal number norelativenumber
    autocmd InsertLeave * setlocal relativenumber
    autocmd WinEnter    * setlocal cursorline
    autocmd WinLeave    * setlocal nocursorline
    autocmd InsertEnter * setlocal nocursorline
    autocmd InsertLeave * setlocal cursorline
  augroup END

  " Remove whitespace on save
  augroup whitespace
    autocmd!
    autocmd BufWritePre * :%s/\s\+$//e
  augroup END

  " Periodically check for file changes
  augroup checktime
    autocmd!
    autocmd CursorHold * silent! checktime
  augroup END

  " Move to the second line of a git commit
  augroup move-to-top-of-commit
    autocmd!
    au FileType gitcommit au! BufEnter COMMIT_EDITMSG call setpos('.', [0, 2, 1, 0])
  augroup END

  " Resize splits when vim changes size (like with tmux opening/closing)
  augroup auto-resize
    autocmd!
    autocmd VimResized * wincmd =
  augroup END

  " Autocomplete
  augroup omnifuncs
    autocmd!
    autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
    autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
    autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
    autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
    autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags
  augroup endif
endif
